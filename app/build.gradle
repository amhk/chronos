apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 'android-P'
    defaultConfig {
        applicationId "amhk.chronos"
        versionCode 1
        versionName "1.0"
        minSdkVersion 24
        //// this issues a warning that minSdkVersion(24) is
        //// greater than targetSdkVersion(1)?!? So, 'P' is 1!
        //targetSdkVersion 'P'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        test {
            java.srcDirs += 'src/test/kotlin'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    implementation "com.google.android.material:material:${versions.androidx}"
    implementation "androidx.annotation:annotation:${versions.androidx}"
    implementation "androidx.constraintlayout:constraintlayout:1.1.0"
    implementation "androidx.core:core-ktx:${versions.androidx}"
    implementation "androidx.lifecycle:lifecycle-extensions:${versions.jetpack}"
    implementation "androidx.lifecycle:lifecycle-livedata:${versions.jetpack}"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "androidx.room:room-runtime:${versions.jetpack}"
    kapt "androidx.room:room-compiler:${versions.jetpack}"
    androidTestImplementation "androidx.room:room-testing:${versions.jetpack}"

    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    ktlint "com.github.shyiko:ktlint:0.23.1"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
